import org.gradle.internal.jvm.Jvm

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion sdk_target.toInteger()
    buildToolsVersion build_tools
    defaultConfig {
        minSdkVersion sdk_min.toInteger()
        targetSdkVersion sdk_target.toInteger()
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs 'src'
        }
        androidTest {
            setRoot('tests')
            manifest.srcFile 'tests/AndroidManifest.xml'
            assets.srcDir 'tests/assets'
            java.srcDir 'tests/src'
            res.srcDir 'tests/res'
            resources.srcDir 'tests/src'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin"
    compile project(':bundler-annotations')
    compile "com.android.support:support-fragment:$support"

    androidTestCompile "com.google.truth:truth:$google_truth"
    androidTestCompile("com.android.support.test:runner:$runner") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    testCompile "junit:junit:$junit"
    testCompile "com.google.truth:truth:$google_truth"
    testCompile "com.google.testing.compile:compile-testing:$google_compile"
    testCompile files(getRuntimeJar())
    testCompile files(Jvm.current().getToolsJar())
    testCompile project(':bundler-compiler')
}

static def getRuntimeJar() {
    try {
        final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile();
        File runtimeJar = new File(javaBase, "lib/rt.jar");
        if (runtimeJar.exists())
            return runtimeJar;
        runtimeJar = new File(javaBase, "jre/lib/rt.jar");
        return runtimeJar.exists() ? runtimeJar : null;
    } catch (IOException e) {
        throw new RuntimeException(e);
    }
}

publish {
    userOrg bintray_user
    groupId bintray_group
    artifactId bintray_artifact
    publishVersion bintray_release
    desc bintray_desc
    website bintray_web
}